#!/usr/bin/perl -w
#========================================================================
#
# git-commit-log
#
# DESCRIPTION
#
# When run in a git repository, generates a listing of git commit ids
# organized by release tags.
#
# AUTHOR
#   Bryce W Harrington <bryce@bryceharrington.org>
#
# COPYRIGHT
#   Copyright (C) 2008 Bryce W. Harrington
#   All Rights Reserved.
#
#   This program is free software; you can redistribute it and/or
#   modify it under the same terms as Perl itself.
#
#========================================================================

use strict;
use Pod::Usage;
use Getopt::Long qw(:config no_ignore_case bundling);

#------------------------------------------------------------------------
# Global variables
#------------------------------------------------------------------------
use vars qw($VERSION $NAME $YEAR);
$VERSION                  = '1.01';
$NAME                     = 'git-commit-log';
$YEAR                     = '2008';

our $opt_version          = 0;
our $opt_help             = 0;
our $opt_helplong         = 0;
our $opt_man              = 0;
our $opt_commits          = 0;
our $opt_interval         = 1;
our $opt_skip             = 0;
our $opt_begin;
our $opt_end;
Getopt::Long::Configure ("bundling", "no_ignore_case");
GetOptions(
           "version|V",
           "help|h",
           "helplong|H",
           "man|M",
           "begin|b=s",
           "commits|c",
           "end|e=s",
           "interval|i=n",
           "skip|s",
           ) or pod2usage(-verbose => 0, -exitstatus => 0);

version_and_exit() if $opt_version;
pod2usage(-verbose => 0, -exitstatus => 0) if $opt_help;
pod2usage(-verbose => 1, -exitstatus => 0) if $opt_helplong;
pod2usage(-verbose => 0, -exitstatus => 1) if (! $ARGV[0]);

sub usage {
    print "$0 [-c] <package>\n";
    print "Must be run from inside a git repository\n";
    print "Examples:\n";
    print "  $0 xf86-video-intel\n";
    print "  $0 nv\n";
    print "\n";
    print "Options:\n";
    print "  -b CMMT  Start with given commit id\n";
    print "  -c       Only display commit id's\n";
    print "  -e CMMT  Stop with given commit id\n";
    print "  -i N     Interval level\n";
    print "  -s       Skip printing non-interval commits\n";
    exit 0;
}

my $package = shift @ARGV || usage();
my @commits = (split(/\n/, `git tag -l "$package-*" `), 'master');

my $prev = pop @commits;
my $display = $prev;
my $select = '';
my $count = 0;
my $do_print = ($opt_begin || $opt_end)? undef : 1;
while (my $next = pop @commits) {
    my ($commit, $date, $time, $comment);
    foreach my $line (`git log $next..$prev | grep -A2 commit`) {
        next if $line =~ /^Author/ or $line =~ /^--/;
        if ($line =~ /commit\s*(\w+)/) {
            $commit = $1;
        } elsif ($line =~ /^Date:\s*\w{3} (\w{3} \d+) (\d\d:\d\d):\d\d (\d{4})/) {
            my $short = sprintf("%.8s", $commit);
            $date = $3 . ' ' . $1;
            $time = $2;
            $count++;
            if (!$display && $count % $opt_interval) {
                next if $opt_skip;
                $select = '      ';
            } elsif (!$display && $count % ($opt_interval*2)) {
                $select = '    ';
            } elsif (!$display && $count % ($opt_interval*4)) {
                $select = '   ';
            } elsif (!$display && $count % ($opt_interval*8)) {
                $select = '  ';
            } elsif (!$display) {
                $select = ' ';
            } else {
                $select = '';
            }

            if ($opt_begin && $commit =~ /^$opt_begin/) {
                $do_print = 0;
                print "Ending printing\n";
            } elsif ($opt_end && $commit =~ /^$opt_end/) {
                $do_print = 1;
                print "Starting printing\n";
            }

            if ($do_print) {
                if ($opt_commits) {
                    print "$short\n";
                } else {
                    printf("%-46s %-11s %s r%s %s\n", $select . $commit, $date, $time, $short, $display || '');
                }
            }
            $display = undef;
        }
    }
    $prev = $next;
    $display = $prev;
    $display =~ s/$package-//;
    $count = 1;
}

__END__

=head1 SYNOPSIS

git-commit-log [-c] <tag-pattern>

Examples:

  git-commit-log xf86-video-intel

  git-commit-log -c nv

=head1 DESCRIPTION

This program displays a listing of commits and commit date from a git
tree, sorted by releases.  The 'tag-pattern' allows distinguishing
tags that correspond to releases; only tags containing this text will
be used as valid tags.

=head1 OPTIONS

=over 8

=item B<-V>,  B<--version>

Prints the version and exits

=item B<-h>, B<--help>

Prints a brief help message

=item B<-H>, B<--helplong>

Prints a long help message

=item B<--man>

Prints a manual page (detailed help)

=item B<-c>

Display a simple columnar list of commit id's.  This is intended to be
suitable for feeding into other programs, like git-pkg.

=back

See B<pkg-page> -h for a summary of options.

=head1 PREREQUISITES

L<Pod::Usage>,
L<Getopt::Long>

=head1 BUGS

=head1 AUTHOR

Bryce W. Harrington E<lt>bryce@bryceharrington.orgE<gt>

L<http://www.bryceharrington.org|http://www.bryceharrington.org>

=head1 COPYRIGHT

Copyright (C) 2008 Bryce W. Harrington.
All Rights Reserved.

This module is free software; you can redistribute it and/or
modify it under the same terms as Perl itself.

=cut
